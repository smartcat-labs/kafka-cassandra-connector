apply plugin: "java"
apply plugin: "maven-publish"
apply plugin: "eclipse"
apply plugin: "idea"
apply plugin: "checkstyle"

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

version = 1.0
group = "io.smartcat"
description = "Generic Cassandra Sink it allows you to write events from Kafka to Cassandra"

dependencies {
    compile "org.apache.kafka:connect-api:$kafkaVersion"
    compile "org.apache.kafka:connect-runtime:$kafkaVersion"
    compile "org.slf4j:slf4j-api:$slf4jApi"

    compile "com.datastax.cassandra:cassandra-driver-core:$cassandraDriver"

    compile "com.fasterxml.jackson.core:jackson-core:$jacksonVersion"
    compile "com.fasterxml.jackson.core:jackson-databind:$jacksonVersion"
}

jar {
    from sourceSets.main.java.srcDirs
    from {
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact tasks.packageSource
            artifact tasks.packageJavadoc
        }
    }
    repositories {
        maven {
            credentials {
                username project.hasProperty("bintrayUsername") ? bintrayUsername : System.getenv('BINTRAY_USER')
                password project.hasProperty("bintrayPassword") ? bintrayPassword : System.getenv('BINTRAY_API_KEY')
            }
            url "https://api.bintray.com/maven/smartcat-labs/maven/kafka-cassandra-connector/;publish=1"
        }
    }
}

task packageSource(type: Jar) {
    classifier "sources"
    from sourceSets.main.allJava
}

task packageJavadoc(type: Jar) {
    classifier = "javadoc"
    from javadoc
}

build.dependsOn packageJavadoc

checkstyle {
    configFile = file("checkstyle.xml")
    configProperties = properties(file("checkstyle.properties"))
}

def properties(file) {
    Properties props = new Properties()
    props.load(new FileInputStream(file))
    props
}
